type target = Seller | Buyer | Admin
module Query = %relay(`
  query Maintenance_getSystemMaintenance_Query {
    systemMaintenance {
      id
      endAt
      startAt
      message
      targets
    }
  }
`)

module View = {
  @react.component
  let make = (~message, ~maintenanceTime) => {
    <section
      className=%twc("w-screen h-screen flex flex-col items-center justify-start dialog-overlay")>
      <div className=%twc("flex flex-col h-full items-center justify-center")>
        <img src="/images/maintenance.png" width="140" height="156" />
        <h1 className=%twc("mt-7 text-3xl text-gray-800 whitespace-pre text-center")>
          {`ë” ë‚˜ì€ ì„œë¹„ìŠ¤ë¥¼ ìœ„í•´ì„œ\nì‹œìŠ¤í…œ ì ê²€ ì¤‘ìž…ë‹ˆë‹¤`->React.string}
        </h1>
        <div className=%twc("flex flex-col justify-center items-center gap-5")>
          <h3 className=%twc("mt-7 text-[17px]")>
            {message
            ->Option.getWithDefault(`ì ê²€ ì‹œê°„ ë™ì•ˆ ì„œë¹„ìŠ¤ ì´ìš©ì´ ì¼ì‹œ ì¤‘ë‹¨ë©ë‹ˆë‹¤.ì´ìš©ì— ë¶ˆíŽ¸ì„ ë“œë ¤ì„œ ì£„ì†¡í•©ë‹ˆë‹¤.`)
            ->React.string}
          </h3>
          <div className=%twc("flex flex-col items-center py-3 bg-gray-gl rounded-lg w-[320px]")>
            <span className=%twc("")> {`ì ê²€ ì‹œê°„`->React.string} </span>
            <span className=%twc("text-gray-800 font-bold")>
              {maintenanceTime->Option.getWithDefault(`ë¶ˆëŸ¬ì˜¤ëŠ” ì¤‘ìž…ë‹ˆë‹¤`)->React.string}
            </span>
          </div>
        </div>
      </div>
    </section>
  }
}

module Content = {
  type maintenanceTargetMatch =
    | Matched
    | NotMatched

  let formatDate = (date, formatString) => {
    let dateObj = date->Js.Date.fromString
    dateObj->DateFns.format(formatString)
  }

  let formatForDisplay = date =>
    `${date->formatDate("MM")}ì›” ${date->formatDate("dd")}ì¼ ${date->formatDate("HH")}ì‹œ`

  @react.component
  let make = () => {
    // ì ê²€ íŽ˜ì´ì§€ ë¡œì§
    let queryData = Query.use(~variables=(), ())

    let isMaintenanceTarget = (targets, startAt, endAt) => {
      let timeCheck = {
        let now = Js.Date.now()->Js.Date.fromFloat
        // now is after startDate
        DateFns.isAfter(now, startAt->Js.Date.fromString) &&
        //and now is before endDate
        DateFns.isBefore(now, endAt->Js.Date.fromString)
      }
      let targetCheck = {
        Next.Router.useRouter().pathname
        ->Js.String2.split("/")
        ->Array.getBy(x => x !== "")
        ->Option.map(firstPath => targets->Array.some(targetElement => firstPath == targetElement))
        ->Option.getWithDefault(false)
      }

      switch timeCheck && targetCheck {
      | true => Matched
      | false => NotMatched
      }
    }

    switch queryData.systemMaintenance {
    | Some(systemMaintenance) => {
        let {endAt, startAt, message, targets} = systemMaintenance
        switch isMaintenanceTarget(targets, startAt, endAt) {
        | Matched => <>
            <RadixUI.Dialog.Root _open=true>
              <RadixUI.Dialog.Portal>
                <RadixUI.Dialog.Overlay className=%twc("dialog-overlay") />
                <RadixUI.Dialog.Content className=%twc("top-0 bg-white fixed z-20")>
                  <Next.Head>
                    <title> {`ðŸš§ ì‹ ì„ í•˜ì´ ì ê²€ì¤‘ìž…ë‹ˆë‹¤ ðŸš§`->React.string} </title>
                  </Next.Head>
                  <View
                    message
                    maintenanceTime={Some(
                      `${startAt->formatForDisplay} ~ ${endAt->formatForDisplay}`,
                    )}
                  />
                </RadixUI.Dialog.Content>
              </RadixUI.Dialog.Portal>
            </RadixUI.Dialog.Root>
          </>
        | NotMatched => React.null
        }
      }
    | None => React.null
    }
  }
}

@react.component
let make = () => {
  let (isCsr, setIsCsr) = React.Uncurried.useState(_ => false)
  React.useEffect0(() => {
    setIsCsr(._ => true)
    None
  })
  switch isCsr {
  | true => <Content />
  | false => React.null
  }
}
