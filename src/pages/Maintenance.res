module MaintenanceTime = {
  type t = {
    from: Js.Date.t,
    to_: option<Js.Date.t>,
  }

  let make = (from, to_) => {
    let now = Js.Date.now()->Js.Date.fromFloat
    let isBetween = (from, now, to') => {
      DateFns.isAfter(now, from) && DateFns.isBefore(now, to')
    }
    switch (from->Js.Date.fromString, to_->Option.map(Js.Date.fromString)) {
    | (from', Some(to_')) if isBetween(from', now, to_') =>
      Some({
        from: from',
        to_: Some(to_'),
      })

    | (from', None) if DateFns.isAfter(now, from') => Some({from: from', to_: None})
    | _ => None
    }
  }

  let format = ({from, to_}) => {
    let formatForDisplay = date => date->DateFns.format(`MMì›” ddì¼ HHì‹œ`)

    `${from->formatForDisplay} ~ ${to_->Option.mapWithDefault("", formatForDisplay)}`
  }
}

module StatusPageAPI = {
  // statuspageì˜ apiì— ëŒ€í•œ ëª¨ë¸ë§ì…ë‹ˆë‹¤.
  module Component = {
    @spice
    type t = {name: string}
  }

  module IncidentUpdate = {
    @spice
    type t = {
      @spice.key("affected_components") components: option<array<Component.t>>,
      body: string,
      @spice.key("created_at") createdAt: string,
    }
  }
  module Incident = {
    @spice
    type incident = {
      components: array<Component.t>,
      id: string,
      impact: string,
      name: option<string>,
      @spice.key("created_at") createdAt: string,
      @spice.key("scheduled_for") scheduledFor: option<string>,
      @spice.key("scheduled_until") scheduledUntil: option<string>,
      @spice.key("started_at") startedAt: option<string>,
      @spice.key("incident_updates") incidentUpdates: array<IncidentUpdate.t>,
    }
  }

  @spice
  type incidents = array<Incident.incident>

  let useStatusPage = apiEndpoint => {
    let fetcher = url => {
      open FetchHelper

      url->Fetch.fetchWithInit(
        Fetch.RequestInit.make(
          ~method_=Get,
          ~headers=Fetch.HeadersInit.make({
            "Authorization": `OAuth ${Env.statusPageKey}`,
          }),
          (),
        ),
      )
      |> Js.Promise.then_(res =>
        if res->Fetch.Response.ok {
          res->Fetch.Response.json
        } else {
          res->Fetch.Response.json
          |> Js.Promise.then_(errJson => {
            Js.Promise.reject({
              let error = makeError(`ìš”ì²­ì— ì‹¤íŒ¨í–ˆìŠµë‹ˆë‹¤.`)
              error->setStatus(res->Fetch.Response.status)
              error->setInfo(errJson)
              switch errJson->errJson_decode {
              | Ok(errJson') => error->setMessage(errJson'.message)
              | Error(_) => ()
              }
              error->convertToExn
            })
          })
          |> Js.Promise.catch(err => {
            Js.Promise.reject(err->convertToExnFromPromiseError)
          })
        }
      )
      |> Js.Promise.then_(data => Js.Promise.resolve(data))
      |> Js.Promise.catch(_ => Js.Promise.resolve(Js.Json.null))
    }

    let {data, error} = Swr.useSwr(
      `https://api.statuspage.io/v1/pages/${Env.statusPagePageId}/incidents/${apiEndpoint}`,
      fetcher,
      Swr.fetcherOptions(
        ~revalidateIfStale=false,
        ~revalidateOnFocus=false,
        ~revalidateOnReconnect=false,
        ~errorRetryCount=0,
        ~shouldRetryOnError=false,
        (),
      ),
    )

    switch (data, error) {
    | (Some(data), None) =>
      data->incidents_decode->Result.mapWithDefault(None, incident => incident->Some)
    | _ => None
    }
  }
}

module Match = {
  type t = {
    message: option<string>,
    maintenanceTime: MaintenanceTime.t,
  }

  let make = (currentSubPage, incidentTargets, message, from, to_) => {
    let pathCheck = (currentSubPage, incidentTargets) => {
      incidentTargets
      ->Array.getBy(incidentTarget => currentSubPage == incidentTarget)
      ->Option.isSome
    }

    switch (pathCheck(currentSubPage, incidentTargets), MaintenanceTime.make(from, to_)) {
    | (true, Some(maintenanceTime)) =>
      Some([
        {
          message,
          maintenanceTime,
        },
      ])
    | _ => None
    }
  }

  let fromIncident = (incident: StatusPageAPI.Incident.incident, currentSubPage) => {
    // ì´ incidentì˜ ê°€ì¥ ë§ˆì§€ë§‰ ì—…ë°ì´íŠ¸ì˜ ë©”ì‹œì§€ë¥¼ ê°€ì ¸ì˜µë‹ˆë‹¤.
    let message =
      incident.incidentUpdates
      ->SortArray.stableSortBy((incidentA, incidentB) =>
        DateFns.compareDesc(
          incidentA.createdAt->Js.Date.fromString,
          incidentB.createdAt->Js.Date.fromString,
        )
      )
      ->Array.get(0)
      ->Option.map(latestIncident => latestIncident.body)

    let incidentTarget = incident.components->Array.map(component => component.name)

    switch (incident.scheduledFor, incident.scheduledUntil) {
    | (Some(scheduledFor), Some(_)) =>
      // ì˜ˆì •ëœ ì‹œì‘ ì‹œê°„ê³¼ ì˜ˆì •ëœ ì¢…ë£Œ ì‹œê°„ì´ ì œì‹œëœ ê²½ìš° -> Maintenance
      make(currentSubPage, incidentTarget, message, scheduledFor, incident.scheduledUntil)
    | _ =>
      // ì‚¬ê±´ ë°œìƒ ì‹œì‘ ì‹œê°„ë§Œ ì¡´ì¬í•˜ëŠ” ê²½ìš° -> Incident
      make(currentSubPage, incidentTarget, message, incident.createdAt, None)
    }
  }
}

module View = {
  @react.component
  let make = (~message, ~maintenanceTime=?) => {
    <section
      className=%twc(
        "w-screen h-screen flex flex-col items-center justify-start dialog-overlay bg-white pointer-events-none"
      )>
      <div className=%twc("flex flex-col h-full items-center justify-center")>
        <img src="/images/maintenance.png" width="140" height="156" />
        <h1 className=%twc("mt-7 text-3xl text-gray-800 whitespace-pre text-center")>
          {`ë” ë‚˜ì€ ì„œë¹„ìŠ¤ë¥¼ ìœ„í•´ì„œ\nì‹œìŠ¤í…œ ì ê²€ ì¤‘ì…ë‹ˆë‹¤`->React.string}
        </h1>
        <div className=%twc("flex flex-col justify-center items-center gap-5")>
          <h3 className=%twc("mt-7 text-[17px] whitespace-pre text-center")>
            {message
            ->Option.getWithDefault(`ì ê²€ ì‹œê°„ ë™ì•ˆ ì„œë¹„ìŠ¤ ì´ìš©ì´ ì¼ì‹œ ì¤‘ë‹¨ë©ë‹ˆë‹¤.\nì´ìš©ì— ë¶ˆí¸ì„ ë“œë ¤ì„œ ì£„ì†¡í•©ë‹ˆë‹¤.`)
            ->React.string}
          </h3>
          <div className=%twc("flex flex-col items-center py-3 bg-gray-gl rounded-lg w-[320px]")>
            <span className=%twc("")> {`ì ê²€ ì‹œê°„`->React.string} </span>
            <span className=%twc("text-gray-800 font-bold")>
              {maintenanceTime
              ->Option.map(MaintenanceTime.format)
              ->Option.getWithDefault(`ë¶ˆëŸ¬ì˜¤ëŠ” ì¤‘ì…ë‹ˆë‹¤`)
              ->React.string}
            </span>
          </div>
        </div>
      </div>
    </section>
  }
}

module Container = {
  @react.component
  let make = () => {
    // ì ê²€ í˜ì´ì§€ ë¡œì§
    let router = Next.Router.useRouter()
    let routerPathNames = router.pathname->Js.String2.split("/")
    let currentSubPage = switch (routerPathNames->Array.get(1), routerPathNames->Array.get(2)) {
    | (Some("seller"), _) => "SELLER"
    | (Some("admin"), _) => "ADMIN"
    | (Some("buyer"), Some("products"))
    | (Some("buyer"), Some(_)) => "BUYER"
    | (Some("buyer"), None)
    | (_, _) => "COMMON"
    }

    let incidents = StatusPageAPI.useStatusPage("unresolved")
    let maintenance = StatusPageAPI.useStatusPage("scheduled")

    let currentIncident =
      switch (incidents, maintenance) {
      | (Some(incidents'), Some(maintenance')) => incidents'->Js.Array.concat(maintenance')
      | (Some(incidents'), None) => incidents'
      | (None, Some(maintenance')) => maintenance'
      | (None, None) => []
      }
      ->Array.keepMap(incidentList => Match.fromIncident(incidentList, currentSubPage))
      ->Array.get(0)

    switch currentIncident {
    | Some([{message, maintenanceTime}, _])
    | Some([{message, maintenanceTime}]) =>
      <RadixUI.Dialog.Root _open=true>
        <RadixUI.Dialog.Portal>
          <RadixUI.Dialog.Overlay className=%twc("dialog-overlay") />
          <RadixUI.Dialog.Content className=%twc("top-0 bg-white fixed z-20")>
            <Next.Head>
              <title> {`ğŸš§ ì‹ ì„ í•˜ì´ ì ê²€ì¤‘ì…ë‹ˆë‹¤ ğŸš§`->React.string} </title>
            </Next.Head>
            <View message maintenanceTime />
          </RadixUI.Dialog.Content>
        </RadixUI.Dialog.Portal>
      </RadixUI.Dialog.Root>
    | _ => React.null
    }
  }
}

@react.component
let make = () => {
  let (isCsr, setIsCsr) = React.Uncurried.useState(_ => false)
  React.useEffect0(() => {
    setIsCsr(._ => true)
    None
  })
  switch isCsr {
  | true => <Container />
  | false => React.null
  }
}
