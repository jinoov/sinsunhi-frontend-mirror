open ReactHookForm
module Select_Unit = Select_Product_Option_Unit

@module("../../public/assets/checkbox-checked.svg")
external checkboxCheckedIcon: string = "default"

@module("../../public/assets/checkbox-unchecked.svg")
external checkboxUncheckedIcon: string = "default"

module Form = {
  @spice
  type cost = {
    @spice.key("raw-cost") rawCost: int,
    @spice.key("working-cost") workingCost: int,
    @spice.key("delivery-cost") deliveryCost: option<int>,
    @spice.key("cost-type") costType: string,
    @spice.key("buyer-price") buyerPrice: int,
  }

  @spice
  type submit = {
    name: option<string>,
    grade: option<string>,
    package: option<string>,
    weight: float,
    each: option<Product_Option_Each_Admin.Form.each>,
    @spice.key("weight-unit") weightUnit: Select_Unit.Weight.status,
    @spice.key("operation-status") operationStatus: string,
    cost: cost,
    @spice.key("cut-off-time") cutOffTime: option<string>,
    @spice.key("memo") memo: option<string>,
    @spice.key("show-each") showEach: bool,
    @spice.key("is-free-shipping") isFreeShipping: Select_Product_Shipping_Type.status,
  }

  type inputNames = {
    name: string,
    autoCompleteName: string,
    grade: string,
    package: string,
    weight: string,
    weightUnit: string,
    operationStatus: string,
    isFreeShipping: string,
    buyerPrice: string,
    cost: string,
    rawCost: string,
    workingCost: string,
    deliveryCost: string,
    costType: string,
    cutOffTime: string,
    memo: string,
    each: string,
    showEach: string,
  }

  let makeNames = prefix => {
    name: `${prefix}.name`,
    autoCompleteName: `${prefix}.autoCompleteName`,
    grade: `${prefix}.grade`,
    package: `${prefix}.package`,
    weight: `${prefix}.weight`,
    weightUnit: `${prefix}.weight-unit`,
    operationStatus: `${prefix}.operation-status`,
    isFreeShipping: `${prefix}.is-free-shipping`,
    buyerPrice: `${prefix}.cost.buyer-price`,
    cost: `${prefix}.cost.cost`,
    rawCost: `${prefix}.cost.raw-cost`,
    workingCost: `${prefix}.cost.working-cost`,
    deliveryCost: `${prefix}.cost.delivery-cost`,
    costType: `${prefix}.cost.cost-type`,
    cutOffTime: `${prefix}.cut-off-time`,
    memo: `${prefix}.memo`,
    each: `${prefix}.each`,
    showEach: `${prefix}.show-each`,
  }

  let names = {
    name: "name",
    autoCompleteName: "autoCompleteName",
    grade: "grade",
    package: "package",
    weight: "weight",
    weightUnit: "weight-unit",
    operationStatus: "operation-status",
    isFreeShipping: "is-free-shipping",
    buyerPrice: "cost.buyer-price",
    cost: "cost.cost",
    rawCost: "cost.raw-cost",
    workingCost: "cost.working-cost",
    deliveryCost: "cost.delivery-cost",
    costType: "cost.cost-type",
    cutOffTime: "cut-off-time",
    memo: "memo",
    each: "each",
    showEach: "show-each",
  }

  let defaultValue =
    [
      (names.name, Js.Json.null),
      (names.autoCompleteName, Js.Json.null),
      (names.grade, Js.Json.null),
      // TODO: valueAsNumber 제거 후 resolver 에서 validation 처리하기
      // (names.weight, Js.Json.null),
      (names.weightUnit, Select_Unit.WeightStatus.KG->Select_Unit.Weight.status_encode),
      (names.operationStatus, Js.Json.null),
      (names.isFreeShipping, Js.Json.null),
      (names.buyerPrice, Js.Json.null),
      (
        "cost",
        [
          ("raw-cost", Js.Json.null),
          ("working-cost", Js.Json.null),
          ("delivery-cost", Js.Json.null),
          ("buyer-price", Js.Json.null),
        ]
        ->Js.Dict.fromArray
        ->Js.Json.object_,
      ),
      (
        names.cutOffTime,
        Js.Json.string(`10시 이전 발주 완료건에 한해 당일 출고(단, 산지 상황에 따라 출고 일정은 변경 될 수 있습니다.)`),
      ),
      (names.memo, Js.Json.null),
    ]
    ->Js.Dict.fromArray
    ->Js.Json.object_
}

let makeAutoGeneratedName = (
  ~grade,
  ~package,
  ~weight,
  ~weightUnit,
  ~numMin,
  ~numMax,
  ~perWeightUnit,
  ~sizeMin,
  ~sizeMax,
  ~sizeUnit,
  ~showEach,
  (),
) => {
  let basicNames = [
    Helper.Option.map2(weight->Option.flatMap(Float.fromString), weightUnit, (w, wu) =>
      `${w->Float.toString}${wu}`
    ),
    grade->Option.flatMap(str => str === "" ? None : Some(str)),
    package->Option.flatMap(str => str === "" ? None : Some(str)),
  ]

  // 입수정보가 체크되어 있으면 자동생성에 반영한다.
  let additiveNames = switch showEach {
  | true => [
      Helper.Option.map2(numMin, numMax, (min, max) => `상자당 ${min}~${max}`),
      switch (weight, weightUnit, numMin, numMax, perWeightUnit) {
      | (Some(weight'), Some(weightUnit'), Some(numMin'), Some(numMax'), Some(unit)) =>
        switch (
          weight'->Float.fromString,
          weightUnit'->Js.Json.string->Select_Unit.Weight.status_decode,
          numMin'->Float.fromString,
          numMax'->Float.fromString,
          unit->Js.Json.string->Select_Unit.Weight.status_decode,
        ) {
        | (Some(weight''), Ok(weightUnit''), Some(numMin''), Some(numMax''), Ok(unit'')) => {
            let {getPerWeight} = module(Product_Option_Each_Admin)
            Some(
              `${getPerWeight(weight'', weightUnit'', numMax'', Some(unit''))}~` ++
              `${getPerWeight(weight'', weightUnit'', numMin'', Some(unit''))}${unit}`,
            )
          }
        | _ => None
        }
      | _ => None
      },
      switch (sizeMin, sizeMax, sizeUnit) {
      | (Some(min), Some(max), Some(unit)) => Some(`${min}~${max}${unit}`)
      | _ => None
      },
    ]
  | false => []
  }

  basicNames->Array.concat(additiveNames)->Array.keep(Option.isSome)->Js.Array2.joinWith("/")
}

module NameInput = {
  @react.component
  let make = (~inputName) => {
    let {register} = Hooks.Context.use(. ~config=Hooks.Form.config(~mode=#onChange, ()), ())
    let {ref, name, onChange, onBlur} = register(. inputName, None)

    <input
      id=name
      ref
      name
      onChange
      onBlur
      className=%twc("px-3 py-2 border border-border-default-L1 rounded-lg h-9 w-1/3 max-w-sm")
      placeholder=`단품명 입력(커스텀)`
    />
  }
}

module AutoGeneratedName = {
  @react.component
  let make = (~inputNames: Form.inputNames) => {
    let {control} = Hooks.Context.use(. ~config=Hooks.Form.config(~mode=#onChange, ()), ())

    let watchValues = Hooks.WatchValues.use(
      Hooks.WatchValues.NullableTexts,
      ~config=Hooks.WatchValues.config(
        ~control,
        ~name=[
          inputNames.grade,
          inputNames.package,
          inputNames.weight,
          inputNames.weightUnit,
          `${inputNames.each}.${Product_Option_Each_Admin.Form.names.minNum}`,
          `${inputNames.each}.${Product_Option_Each_Admin.Form.names.maxNum}`,
          `${inputNames.each}.${Product_Option_Each_Admin.Form.names.unitWeight}`,
          `${inputNames.each}.${Product_Option_Each_Admin.Form.names.minSize}`,
          `${inputNames.each}.${Product_Option_Each_Admin.Form.names.maxSize}`,
          `${inputNames.each}.${Product_Option_Each_Admin.Form.names.unitSize}`,
        ],
        (),
      ),
      (),
    )

    let showEach = Hooks.WatchValues.use(
      Hooks.WatchValues.Checkbox,
      ~config=Hooks.WatchValues.config(~control, ~name=inputNames.showEach, ()),
      (),
    )

    let generatedName = switch watchValues {
    | Some(values) =>
      switch values->Array.map(Js.Nullable.toOption) {
      | [
          grade,
          package,
          weight,
          weightUnit,
          numMin,
          numMax,
          perWeightUnit,
          sizeMin,
          sizeMax,
          sizeUnit,
        ] =>
        makeAutoGeneratedName(
          ~grade,
          ~package,
          ~weight,
          ~weightUnit,
          ~numMin,
          ~numMax,
          ~perWeightUnit,
          ~sizeMin,
          ~sizeMax,
          ~sizeUnit,
          ~showEach={showEach->Option.getWithDefault(false)},
          (),
        )
      | _ => ""
      }
    | None => ""
    }

    <div
      className=%twc(
        "w-1/3 max-w-[320px] h-9 px-3 py-2 border border-gray-300 bg-gray-100 rounded-lg "
      )>
      <span className=%twc("whitespace-nowrap text-gray-500")>
        {(
          generatedName === "" ? `자동생성 단품명(자동으로 생성)` : generatedName
        )->React.string}
      </span>
    </div>
  }
}

module OptionCode = {
  @react.component
  let make = () => {
    <div
      className=%twc(
        "w-1/3 max-w-[162px] h-9  px-3 py-2 border border-gray-300 bg-gray-100 rounded-lg"
      )>
      <span className=%twc("whitespace-nowrap text-gray-500")>
        {`단품코드(자동생성)`->React.string}
      </span>
    </div>
  }
}

module GradeInput = {
  @react.component
  let make = (~inputName) => {
    let {register} = Hooks.Context.use(. ~config=Hooks.Form.config(~mode=#onChange, ()), ())
    let {ref, name, onChange, onBlur} = register(. inputName, None)

    <div className=%twc("w-1/3 max-w-[320px]")>
      <label htmlFor=name className=%twc("block font-bold")>
        {`등급(용도)`->React.string}
      </label>
      <input
        id=name
        ref
        name
        onChange
        onBlur
        className=%twc("mt-2 w-full h-9 px-3 py-2 border border-gray-300 rounded-lg")
        placeholder=`등급 또는 용도 입력(선택사항)`
      />
    </div>
  }
}

module PackageInput = {
  @react.component
  let make = (~inputName) => {
    let {register} = Hooks.Context.use(. ~config=Hooks.Form.config(~mode=#onChange, ()), ())
    let {ref, name, onChange, onBlur} = register(. inputName, None)

    <div className=%twc("w-1/3 max-w-[320px]")>
      <label htmlFor=name className=%twc("block font-bold")> {`포장재질`->React.string} </label>
      <input
        id=name
        ref
        name
        onChange
        onBlur
        className=%twc("mt-2 w-full h-9 px-3 py-2 border border-gray-300 rounded-lg")
        placeholder=`포장재질(선택사항)`
      />
    </div>
  }
}

module WeightInput = {
  @react.component
  let make = (~showEachInputName, ~weightInputName, ~unitInputName) => {
    let {register, control, formState: {errors}} = Hooks.Context.use(.
      ~config=Hooks.Form.config(~mode=#onChange, ()),
      (),
    )

    let {ref, name, onChange, onBlur} = register(.
      weightInputName,
      Some(Hooks.Register.config(~required=true, ~valueAsNumber=true, ~min=0, ())),
    )

    <>
      <div className=%twc("py-6 flex flex-col gap-2")>
        <label htmlFor=name>
          <span className=%twc("font-bold")> {`중량`->React.string} </span>
          <span className=%twc("text-red-500")> {`*`->React.string} </span>
        </label>
        <div className=%twc("flex items-center gap-2")>
          <input
            ref
            id=name
            name
            type_="number"
            step=0.01
            onChange
            onBlur
            className=%twc("px-3 py-2 border border-border-default-L1 rounded-lg h-9")
            placeholder=`중량 입력`
          />
          <Controller
            name=unitInputName
            control
            defaultValue={Select_Unit.WeightStatus.KG->Select_Unit.Weight.status_encode}
            render={({field: {ref, value, onChange}}) =>
              <Select_Unit.Weight
                forwardRef=ref
                status={value
                ->Select_Unit.Weight.status_decode
                ->Result.getWithDefault(Select_Unit.WeightStatus.KG)}
                onChange={status => {
                  status->Select_Unit.Weight.status_encode->Controller.OnChangeArg.value->onChange
                }}
              />}
          />
          <div className=%twc("flex gap-2 items-center grow")>
            <Controller
              name=showEachInputName
              control
              defaultValue={false->Js.Json.boolean}
              render={({field: {name, onChange, value}}) => <>
                <Checkbox
                  checked={value->Js.Json.decodeBoolean->Option.getWithDefault(false)}
                  id=name
                  onChange={e => e->Controller.OnChangeArg.event->onChange}
                />
                <label htmlFor=name> {`입수 정보 입력`->React.string} </label>
              </>}
            />
          </div>
        </div>
        <ErrorMessage
          errors
          name
          render={_ =>
            <span className=%twc("flex")>
              <IconError width="20" height="20" />
              <span className=%twc("text-sm text-notice ml-1")>
                {`중량을 입력해주세요.(음수입력 불가)`->React.string}
              </span>
            </span>}
        />
      </div>
    </>
  }
}

module PriceInput = {
  @react.component
  let make = (~inputName) => {
    let {register, formState: {errors}} = Hooks.Context.use(.
      ~config=Hooks.Form.config(~mode=#onChange, ()),
      (),
    )

    let {ref, name, onChange, onBlur} = register(.
      inputName,
      Some(Hooks.Register.config(~required=true, ~valueAsNumber=true, ~min=0, ())),
    )

    <div className=%twc("flex flex-col w-[158px] min-w-[158px]")>
      <label className=%twc("block")>
        <span className=%twc("font-bold")> {`바이어판매가`->React.string} </span>
        <span className=%twc("text-red-500")> {`*`->React.string} </span>
      </label>
      <input
        id=name
        ref
        type_="number"
        name
        onChange
        onBlur
        className=%twc("mt-2 w-full h-9 px-3 py-2 border border-gray-300 rounded-lg")
        placeholder=`가격입력`
      />
      <ErrorMessage
        errors
        name
        render={_ =>
          <span className=%twc("flex")>
            <IconError width="20" height="20" />
            <span className=%twc("text-sm text-notice ml-1")>
              {`바이어판매가를 입력해주세요.`->React.string}
            </span>
          </span>}
      />
    </div>
  }
}
module OptionStatusSelect = {
  @react.component
  let make = (~inputName) => {
    let {control, formState: {errors}} = Hooks.Context.use(.
      ~config=Hooks.Form.config(~mode=#onChange, ()),
      (),
    )

    <div className=%twc("flex flex-col w-[158px] min-w-[158px]")>
      <label className=%twc("block")>
        <span className=%twc("font-bold")> {`운영상태`->React.string} </span>
        <span className=%twc("text-red-500")> {`*`->React.string} </span>
      </label>
      <div className=%twc("mt-2 w-full h-9")>
        <Controller
          name=inputName
          control
          rules={Rules.make(~required=true, ())}
          render={({field: {ref, name, value, onChange}}) =>
            <div>
              <Select_Product_Operation_Status.Base
                forwardRef=ref
                status={value
                ->Select_Product_Operation_Status.Base.status_decode
                ->Result.mapWithDefault(None, v => Some(v))}
                onChange={status => {
                  status
                  ->Select_Product_Operation_Status.Base.status_encode
                  ->Controller.OnChangeArg.value
                  ->onChange
                }}
              />
              <ErrorMessage
                errors
                name
                render={_ =>
                  <span className=%twc("flex")>
                    <IconError width="20" height="20" />
                    <span className=%twc("text-sm text-notice ml-1")>
                      {`운영상태를 입력해주세요.`->React.string}
                    </span>
                  </span>}
              />
            </div>}
        />
      </div>
    </div>
  }
}

module IsFreeShipping = {
  @react.component
  let make = (~inputName) => {
    let {control, formState: {errors}} = Hooks.Context.use(.
      ~config=Hooks.Form.config(~mode=#onChange, ()),
      (),
    )

    let toStatus = statusFromSelect => {
      statusFromSelect
      ->Select_Product_Shipping_Type.status_decode
      ->Result.mapWithDefault(None, v => Some(v))
    }

    <div className=%twc("flex flex-col w-[158px] min-w-[158px]")>
      <label className=%twc("block")>
        <span className=%twc("font-bold")> {`배송비 타입`->React.string} </span>
        <span className=%twc("text-red-500")> {`*`->React.string} </span>
      </label>
      <span className=%twc("mt-2 w-full h-9")>
        <Controller
          name=inputName
          control
          rules={Rules.make(~required=true, ())}
          render={({field: {ref, name, value, onChange}}) => {
            <div>
              <Select_Product_Shipping_Type
                forwardRef=ref
                status={value->toStatus}
                onChange={selected => {
                  selected
                  ->Select_Product_Shipping_Type.status_encode
                  ->Controller.OnChangeArg.value
                  ->onChange
                }}
              />
              <ErrorMessage
                errors
                name
                render={_ => {
                  <span className=%twc("flex")>
                    <IconError width="20" height="20" />
                    <span className=%twc("text-sm text-notice ml-1")>
                      {`배송비 타입을 입력해주세요.`->React.string}
                    </span>
                  </span>
                }}
              />
            </div>
          }}
        />
      </span>
    </div>
  }
}

module RawCostInput = {
  @react.component
  let make = (~inputName) => {
    let {register, formState: {errors}} = Hooks.Context.use(.
      ~config=Hooks.Form.config(~mode=#onChange, ()),
      (),
    )

    let {ref, name, onChange, onBlur} = register(.
      inputName,
      Some(Hooks.Register.config(~required=true, ~valueAsNumber=true, ~min=0, ())),
    )

    <div className=%twc("flex flex-col w-[158px] min-w-[158px]")>
      <label className=%twc("block") htmlFor=name>
        <span className=%twc("font-bold")> {`생산자 원물원가`->React.string} </span>
        <span className=%twc("text-red-500")> {`*`->React.string} </span>
      </label>
      <input
        ref
        id=name
        type_="number"
        name
        onChange
        onBlur
        className=%twc(
          "mt-2 w-full h-9 px-3 py-2 border border-gray-300 rounded-lg focus:outline-none"
        )
        placeholder=`가격입력`
      />
      <ErrorMessage
        errors
        name
        render={_ =>
          <span className=%twc("flex")>
            <IconError width="20" height="20" />
            <span className=%twc("text-sm text-notice ml-1")>
              {`생산자원물원가를 입력해주세요.`->React.string}
            </span>
          </span>}
      />
    </div>
  }
}

module WorkingCostInput = {
  @react.component
  let make = (~inputName) => {
    let {register, formState: {errors}} = Hooks.Context.use(.
      ~config=Hooks.Form.config(~mode=#onChange, ()),
      (),
    )

    let {ref, name, onChange, onBlur} = register(.
      inputName,
      Some(Hooks.Register.config(~required=true, ~valueAsNumber=true, ~min=0, ())),
    )

    <div className=%twc("flex flex-col w-[158px] min-w-[158px]")>
      <label className=%twc("block") htmlFor=name>
        <span className=%twc("font-bold")> {`생산자 포장작업비`->React.string} </span>
        <span className=%twc("text-red-500")> {`*`->React.string} </span>
      </label>
      <input
        ref
        id=name
        type_="number"
        name
        onChange
        onBlur
        className=%twc(
          "mt-2 w-full h-9 px-3 py-2 border border-gray-300 rounded-lg focus:outline-none"
        )
        placeholder=`가격입력`
      />
      <ErrorMessage
        errors
        name
        render={_ =>
          <span className=%twc("flex")>
            <IconError width="20" height="20" />
            <span className=%twc("text-sm text-notice ml-1")>
              {`생산자 포장작업비를 입력해주세요.`->React.string}
            </span>
          </span>}
      />
    </div>
  }
}

module DeliveryCostInput = {
  module Disabled = {
    @react.component
    let make = () => {
      <div className=%twc("flex flex-col w-[158px] min-w-[158px] gap-2")>
        <span className=%twc("font-bold block")> {`생산자 택배비`->React.string} </span>
        <div className=%twc("h-9 w-full px-3 py-2 border border-gray-300 bg-gray-100 rounded-lg")>
          <span className=%twc("text-disabled-L2")> {`택배 불가능`->React.string} </span>
        </div>
      </div>
    }
  }

  @react.component
  let make = (~inputName) => {
    let {register, formState: {errors}} = Hooks.Context.use(.
      ~config=Hooks.Form.config(~mode=#onChange, ()),
      (),
    )

    let {ref, name, onChange, onBlur} = register(.
      inputName,
      Some(Hooks.Register.config(~required=true, ~valueAsNumber=true, ~min=0, ())),
    )

    <div className=%twc("flex flex-col w-[158px] min-w-[158px]")>
      <label className=%twc("block") htmlFor=name>
        <span className=%twc("font-bold")> {`생산자 택배비`->React.string} </span>
        <span className=%twc("text-red-500")> {`*`->React.string} </span>
      </label>
      <input
        ref
        id=name
        type_="number"
        name
        onChange
        onBlur
        className=%twc(
          "mt-2 w-full h-9 px-3 py-2 border border-gray-300 rounded-lg focus:outline-none"
        )
        placeholder=`가격입력`
      />
      <ErrorMessage
        errors
        name
        render={_ =>
          <span className=%twc("flex")>
            <IconError width="20" height="20" />
            <span className=%twc("text-sm text-notice ml-1")>
              {`생산자 택배비를 입력해주세요.`->React.string}
            </span>
          </span>}
      />
    </div>
  }
}

module TotalRawCost = {
  @react.component
  let make = (~inputNames: Form.inputNames) => {
    let watchCost = Hooks.WatchValues.use(
      Hooks.WatchValues.Texts,
      ~config=Hooks.WatchValues.config(
        ~name=[inputNames.rawCost, inputNames.workingCost, inputNames.deliveryCost],
        (),
      ),
      (),
    )

    let toFiniteFloat = optionStr =>
      optionStr->Option.flatMap(Float.fromString)->Option.keep(Js.Float.isFinite)

    let totalRawCost = switch watchCost {
    | Some([rawCost, workingCost, deliveryCost]) => {
        let rawCost' = rawCost->toFiniteFloat->Option.getWithDefault(0.)
        let workingCost' = workingCost->toFiniteFloat->Option.getWithDefault(0.)
        let deliveryCost' = deliveryCost->toFiniteFloat->Option.getWithDefault(0.)
        rawCost' +. workingCost' +. deliveryCost'
      }
    | _ => 0.
    }

    <div className=%twc("flex flex-col w-[158px] min-w-[158px]")>
      <label className=%twc("whitespace-nowrap block")>
        <span className=%twc("font-bold")> {`생산자 총 공급원가`->React.string} </span>
        <span className=%twc("text-gray-600")> {` *자동계산`->React.string} </span>
      </label>
      <div className=%twc("mt-2 h-9 w-full px-3 py-2 border border-gray-300 rounded-lg")>
        {totalRawCost->Float.toString->React.string}
      </div>
    </div>
  }
}

module CostTypeSelect = {
  @react.component
  let make = (~inputName) => {
    let {control, formState: {errors}} = Hooks.Context.use(.
      ~config=Hooks.Form.config(~mode=#onChange, ()),
      (),
    )

    <div className=%twc("flex flex-col w-[158px] min-w-[158px]")>
      <label className=%twc("block")>
        <span className=%twc("font-bold")> {`생산자 공급가 타입`->React.string} </span>
        <span className=%twc("text-red-500")> {`*`->React.string} </span>
      </label>
      <div className=%twc("mt-2 w-full h-9")>
        <Controller
          name=inputName
          control
          rules={Rules.make(~required=true, ())}
          render={({field: {ref, name, value, onChange}}) =>
            <div>
              <Select_Producer_Contract_Type
                forwardRef=ref
                status={value
                ->Select_Producer_Contract_Type.status_decode
                ->Result.mapWithDefault(None, v => v->Some)}
                onChange={status => {
                  status
                  ->Select_Producer_Contract_Type.status_encode
                  ->Controller.OnChangeArg.value
                  ->onChange
                }}
              />
              <ErrorMessage
                errors
                name
                render={_ =>
                  <span className=%twc("flex")>
                    <IconError width="20" height="20" />
                    <span className=%twc("text-sm text-notice ml-1")>
                      {`공급가 타입을 입력해주세요.`->React.string}
                    </span>
                  </span>}
              />
            </div>}
        />
      </div>
    </div>
  }
}

module CutOffTimeInput = {
  @react.component
  let make = (~inputName) => {
    let {register, formState: {errors}} = Hooks.Context.use(.
      ~config=Hooks.Form.config(~mode=#onChange, ()),
      (),
    )
    let {ref, name, onChange, onBlur} = register(.
      inputName,
      Some(Hooks.Register.config(~maxLength=100, ())),
    )

    <div className=%twc("flex flex-col gap-2 min-w-1/2 max-w-2xl")>
      <label htmlFor=name>
        <span className=%twc("font-bold")> {`출고기준시간`->React.string} </span>
      </label>
      <textarea
        ref
        id=name
        name
        onChange
        onBlur
        className=%twc(
          "px-3 py-2 border border-border-default-L1 rounded-lg focus:outline-none h-9"
        )
        placeholder=`출고기준시간 입력(최대 100자)`
        defaultValue=`10시 이전 발주 완료건에 한해 당일 출고(단, 산지 상황에 따라 출고 일정은 변경 될 수 있습니다.)`
      />
      <ErrorMessage
        errors
        name
        render={_ =>
          <span className=%twc("flex")>
            <IconError width="20" height="20" />
            <span className=%twc("text-sm text-notice ml-1")>
              {`최대 100자까지 입력가능합니다.`->React.string}
            </span>
          </span>}
      />
    </div>
  }
}

module MemoInput = {
  @react.component
  let make = (~inputName) => {
    let {register, formState: {errors}} = Hooks.Context.use(.
      ~config=Hooks.Form.config(~mode=#onChange, ()),
      (),
    )
    let {ref, name, onChange, onBlur} = register(.
      inputName,
      Some(Hooks.Register.config(~maxLength=100, ())),
    )

    <div className=%twc("flex flex-col gap-2 min-w-1/2 max-w-2xl")>
      <label htmlFor=name>
        <span className=%twc("font-bold")> {`메모`->React.string} </span>
      </label>
      <textarea
        ref
        id=name
        name
        onChange
        onBlur
        className=%twc(
          "px-3 py-2 border border-border-default-L1 rounded-lg focus:outline-none h-9"
        )
        placeholder=`메모사항 입력(최대 100자)`
      />
      <ErrorMessage
        errors
        name
        render={_ =>
          <span className=%twc("flex")>
            <IconError width="20" height="20" />
            <span className=%twc("text-sm text-notice ml-1")>
              {`최대 100자까지 입력가능합니다.`->React.string}
            </span>
          </span>}
      />
    </div>
  }
}

@react.component
let make = (
  ~prefix,
  ~index,
  ~remove,
  ~prepend: Hooks.FieldArray.prepend,
  ~isOnlyOneRemained,
  ~productDisplayName,
  ~applyAll,
  ~setApplyAll,
  ~isCourierAvailable,
) => {
  //prefix 형식 : options.[index]
  let {getValues, control} = Hooks.Context.use(. ~config=Hooks.Form.config(~mode=#onChange, ()), ())

  let values = {
    getValues(. [prefix])->Js.Json.decodeArray->Option.getWithDefault([])->Garter.Array.first
  }
  let (isShowRemove, setShowRemove) = React.Uncurried.useState(_ => Dialog.Hide)

  let inputNames = Form.makeNames(prefix)

  let onClickDelete = ReactEvents.interceptingHandler(_ => setShowRemove(._ => Dialog.Show))

  let onClickCopy = ReactEvents.interceptingHandler(_ => {
    setApplyAll(._ => false)
    switch values {
    | None => ()
    | Some(v) => {
        let focusOptions = Hooks.FieldArray.focusOptions(~shouldFocus=true, ())
        prepend(. v, ~focusOptions, ())
      }
    }
  })

  let onClickApplyAll = ReactEvents.interceptingHandler(_ => setApplyAll(.prev => !prev))

  let showEach = Hooks.WatchValues.use(
    Hooks.WatchValues.Checkbox,
    ~config=Hooks.WatchValues.config(~control, ~name=inputNames.showEach, ()),
    (),
  )

  <div className=%twc("bg-gray-50 border border-gray-200 px-3 py-7 rounded text-sm")>
    <RadixUI.Collapsible.Root defaultOpen={true}>
      // 기본정보
      <section className=%twc("flex flex-col gap-6")>
        <div className=%twc("flex flex-col gap-2")>
          <div className=%twc("flex justify-between")>
            <div className=%twc("flex items-center justify-start")>
              <span className=%twc("block font-bold")> {`단품 기본정보`->React.string} </span>
              <button
                onClick=onClickCopy
                className=%twc("ml-2 px-2 py-1 bg-green-500 text-white focus:outline-none rounded")>
                {`복사하기`->React.string}
              </button>
              <button
                className=%twc("ml-2 px-2 py-1 bg-gray-100 rounded focus:outline-none")
                disabled=isOnlyOneRemained
                onClick=onClickDelete>
                {`삭제하기`->React.string}
              </button>
            </div>
            <RadixUI.Collapsible.Trigger className=%twc("collabsible-trigger")>
              <div className=%twc("flex items-center cursor-pointer relative gap-1")>
                <span className=%twc("underline")> {`단품정보 접기`->React.string} </span>
                <IconArrow
                  height="15" width="15" stroke="#000000" className=%twc("transform -rotate-90")
                />
              </div>
            </RadixUI.Collapsible.Trigger>
          </div>
          // 단품명, 자동생성단품명
          <div className=%twc("flex gap-2")>
            <NameInput inputName=inputNames.name /> <AutoGeneratedName inputNames /> <OptionCode />
          </div>
        </div>
      </section>
      <RadixUI.Collapsible.Content className=%twc("collabsible-content")>
        <div className=%twc("divide-y")>
          // 등급, 포장재질
          <div className=%twc("flex gap-2 py-6")>
            <GradeInput inputName=inputNames.grade /> <PackageInput inputName=inputNames.package />
          </div>
          // 중량
          <WeightInput
            showEachInputName=inputNames.showEach
            weightInputName=inputNames.weight
            unitInputName=inputNames.weightUnit
          />
          {
            // 입수 정보
            switch showEach {
            | Some(true) =>
              <Product_Option_Each_Admin
                prefix weightFormName=inputNames.weight wieghtUnitFormName=inputNames.weightUnit
              />
            | Some(false) | None => React.null
            }
          }
          <div className=%twc("flex flex-col gap-6 py-6 w-full")>
            // 바이어판매가, 공급원가, 운영상태, 배송비 타입
            <div className=%twc("flex gap-4 items-center justify-start")>
              <PriceInput inputName=inputNames.buyerPrice />
              <TotalRawCost inputNames />
              <OptionStatusSelect inputName=inputNames.operationStatus />
              <IsFreeShipping inputName=inputNames.isFreeShipping />
            </div>
            // 원물원가, 포장작업비, 배송비, 공급가타입
            <div className=%twc("flex gap-4 items-center justify-start")>
              <RawCostInput inputName=inputNames.rawCost />
              <WorkingCostInput inputName=inputNames.workingCost />
              {switch isCourierAvailable {
              | true => <DeliveryCostInput inputName=inputNames.deliveryCost />
              | false => <DeliveryCostInput.Disabled />
              }}
              <CostTypeSelect inputName=inputNames.costType />
            </div>
          </div>
          <div className=%twc("flex flex-col gap-6 py-6 w-full")>
            <CutOffTimeInput inputName=inputNames.cutOffTime />
            <MemoInput inputName=inputNames.memo />
            {switch index {
            | 0 =>
              <div className=%twc("flex gap-2 items-center")>
                <button onClick=onClickApplyAll>
                  <img src={applyAll ? checkboxCheckedIcon : checkboxUncheckedIcon} />
                </button>
                <span>
                  {`[${productDisplayName}] 전체 단품에 출고기준시간과 메모 동일하게 적용하기`->React.string}
                </span>
              </div>
            | _ => React.null
            }}
          </div>
        </div>
      </RadixUI.Collapsible.Content>
    </RadixUI.Collapsible.Root>
    <Dialog
      boxStyle=%twc("text-center rounded-2xl")
      isShow={isShowRemove}
      textOnCancel=`닫기`
      textOnConfirm=`삭제`
      kindOfConfirm=Dialog.Negative
      onConfirm={_ => {
        remove(. index)
        setShowRemove(._ => Dialog.Hide)
      }}
      onCancel={_ => {
        setShowRemove(._ => Dialog.Hide)
      }}>
      <p className=%twc("text-gray-500 text-center whitespace-pre-wrap")>
        {`등록중인 단품정보를`->React.string}
        <br />
        {`삭제하시겠어요?`->React.string}
      </p>
    </Dialog>
  </div>
}
